PRINT 'Início do script v2. - (' + CONVERT(VARCHAR,GETDATE(),120) + ' ' + CONVERT(VARCHAR,GETDATE(),114) + ')'
GO

USE ORGANIZE_DB
GO

IF EXISTS(SELECT TOP 1 1 FROM sys.tables WHERE name = 'ComplementoFormaPagamento' AND schema_id = schema_id('fontes'))
BEGIN
	DROP TABLE fontes.ComplementoFormaPagamento
	PRINT 'v2 - 001 - Exclusão da tabela ´fontes.ComplementoFormaPagamento´.'
END
GO

IF EXISTS(SELECT TOP 1 1 FROM sys.tables WHERE name = 'FormaPagamento' AND schema_id = schema_id('fontes'))
BEGIN
	DROP TABLE fontes.FormaPagamento
	PRINT 'v2 - 002 - Exclusão da tabela ´fontes.FormaPagamento´.'
END
GO

IF EXISTS(SELECT TOP 1 1 FROM sys.tables WHERE name = 'TipoPagamento' AND schema_id = schema_id('fontes'))
BEGIN
	DROP TABLE fontes.TipoPagamento
	PRINT 'v2 - 003 - Exclusão da tabela ´fontes.TipoPagamento´.'
END
GO

IF EXISTS(SELECT TOP 1 1 FROM sys.tables WHERE name = 'Bancos' AND schema_id = schema_id('fontes'))
BEGIN
	DROP TABLE fontes.Bancos
	PRINT 'v2 - 004 - Exclusão da tabela ´fontes.Bancos´.'
END
GO

IF EXISTS(SELECT TOP 1 1 FROM sys.views WHERE name = 'vwFormasPagamentos' AND schema_id = schema_id('consultas'))
BEGIN
	DROP VIEW consultas.vwFormasPagamentos
	PRINT 'v2 - 005 - Exclusão da view ´fontes.vwFormasPagamentos´.'
END
GO

CREATE TABLE fontes.Bancos
(
	codigo	INT				NOT NULL	PRIMARY KEY,
	nome	VARCHAR(100)	NOT NULL	UNIQUE
)
PRINT 'v2 - 006 - Criação da tabela ´fontes.Bancos´.'
GO

CREATE TABLE fontes.TipoPagamento
(
	codigo		INT			IDENTITY	PRIMARY KEY,
	descricao	CHAR(30)	NOT NULL	UNIQUE
)
PRINT 'v2 - 007 - Criação da tabela ´fontes.TipoPagamento´.'
GO

CREATE TABLE fontes.FormaPagamento
(
	codigo			INT					IDENTITY PRIMARY KEY,
	identificador	UNIQUEIDENTIFIER	DEFAULT NEWID() UNIQUE,
	nome			VARCHAR(70)			UNIQUE,
	tipo			INT					NOT NULL,
	banco			INT					NULL,
	CONSTRAINT FKTipoPagamento01 FOREIGN KEY(tipo)
		REFERENCES fontes.TipoPagamento(codigo),
	CONSTRAINT FKBancos01 FOREIGN KEY(banco)
		REFERENCES fontes.Bancos(codigo)
)
PRINT 'v2 - 008 - Criação da tabela ´fontes.FormaPagamento´.'
GO

CREATE TABLE fontes.ComplementoFormaPagamento
(
	codigo				INT					NOT NULL	PRIMARY KEY,
	diaPagamentoFatura	TINYINT				NOT NULL	DEFAULT(0),
	diaFechamentoFatura	TINYINT				NOT NULL	DEFAULT(0),
	limite				FLOAT				NOT NULL	DEFAULT(0.0),
	diaDeposito			TINYINT				NOT NULL	DEFAULT(0),
	CONSTRAINT FKFormaPagamento01 FOREIGN KEY(codigo)
		REFERENCES fontes.FormaPagamento(codigo)
)
PRINT 'v2 - 009 - Criação da tabela ´fontes.ComplementoFormaPagamento´.'
GO

SET NOCOUNT ON
GO

INSERT INTO fontes.Bancos(codigo, nome)
VALUES
	(237,'Banco Bradesco S.A.'),
	(341,'Itaú Unibanco S.A.'),
	(260,'Nu Pagamentos S.A (Nubank)')
PRINT 'v2 - 010 - Carga na tabela ´fontes.Bancos´ ('+ CONVERT(VARCHAR, @@ROWCOUNT) +').'
GO

INSERT INTO fontes.TipoPagamento(descricao)
VALUES
	('debito_conta'),
	('credito_conta'),
	('debito_automatico'),
	('cartao_credito'),
	('vale_refeicao'),
	('vale_alimentacao')
PRINT 'v2 - 011 - Carga na tabela ´fontes.TipoPagamento´ ('+ CONVERT(VARCHAR, @@ROWCOUNT) +').'
GO

INSERT INTO fontes.FormaPagamento
	(nome, tipo, banco)
SELECT FormasPagamento.descricao, Tipo.codigo, Banco.codigo
FROM(VALUES
		('Bradesco Débito Automático','debito_automatico','Banco Bradesco S.A.'),
		('Itaú Débito Automático','debito_automatico','Itaú Unibanco S.A.'),
		('NuBank Débito Automático','debito_automatico','Nu Pagamentos S.A (Nubank)'),
		('Bradesco Débito Conta','debito_conta','Banco Bradesco S.A.'),
		('Itaú Débito Conta','debito_conta','Itaú Unibanco S.A.'),
		('NuBank Débito Conta','debito_conta','Nu Pagamentos S.A (Nubank)'),
		('ItauCard','cartao_credito','Itaú Unibanco S.A.'),
		('BradescoCard','cartao_credito','Banco Bradesco S.A.'),
		('Cartão Nubank','cartao_credito','Nu Pagamentos S.A (Nubank)'),
		('Alelo Refeição','vale_refeicao',NULL),
		('Alelo Alimentação','vale_alimentacao',NULL)
	) FormasPagamento(descricao, tipo, banco)
	INNER JOIN fontes.TipoPagamento Tipo ON Tipo.descricao = FormasPagamento.tipo
	LEFT JOIN fontes.Bancos Banco ON Banco.nome = FormasPagamento.banco
PRINT 'v2 - 012 - Carga na tabela ´fontes.FormaPagamento´ ('+ CONVERT(VARCHAR, @@ROWCOUNT) +').'
GO

INSERT INTO fontes.ComplementoFormaPagamento
	(codigo, diaPagamentoFatura, diaFechamentoFatura, limite, diaDeposito)
SELECT Forma.codigo, Compl.diaPagamento, Compl.diaFechamento, Compl.limite, Compl.diaDeposito
FROM (VALUES
		('ItauCard',14,4,40000,0),
		('BradescoCard',1,25,20000,0),
		('Cartão Nubank',15,7,1500,0),
		('Alelo Refeição',0,0,900,28),
		('Alelo Alimentação',0,0,700,28)
	) Compl(descricao, diaPagamento, diaFechamento, limite,diaDeposito)
	INNER JOIN fontes.FormaPagamento Forma ON Forma.nome = Compl.descricao
PRINT 'v2 - 013 - Carga na tabela ´fontes.ComplementoFormaPagamento´ ('+ CONVERT(VARCHAR, @@ROWCOUNT) +').'
GO

CREATE VIEW consultas.vwFormasPagamentos
AS
	SELECT
		Formas.identificador, Formas.nome, Tipos.descricao [tipo],
		Bancos.nome [banco], Compl.diaDeposito, Compl.diaFechamentoFatura, Compl.diaPagamentoFatura, Compl.limite
	FROM fontes.FormaPagamento Formas
		INNER JOIN fontes.TipoPagamento Tipos ON Tipos.codigo = Formas.tipo
		LEFT JOIN fontes.Bancos Bancos ON Bancos.codigo = Formas.banco
		LEFT JOIN fontes.ComplementoFormaPagamento Compl ON Compl.codigo = Formas.codigo
GO
PRINT 'v2 - 014 - Criação da view ´fontes.vwFormasPagamentos´ ('+ CONVERT(VARCHAR, @@ROWCOUNT) +').'
GO

PRINT 'Fim do script v2. - (' + CONVERT(VARCHAR,GETDATE(),120) + ' ' + CONVERT(VARCHAR,GETDATE(),114) + ')'
GO